class Kicker : Inventory
{
    int deflectTime;
    int returnTime;

    double angle;
    double pitch;
    double roll;
    double up;
    Vector2 weaponOfs;

    override void Tick()
    {
        if (!owner || !owner.player) return;

        let psp = owner.player.GetPSprite(PSprite.Weapon);

        double age = gameTic - spawnTime;
        if (age < deflectTime)
        {
            owner.angle += angle / deflectTime;
            owner.pitch += pitch / deflectTime;
            owner.roll += roll / deflectTime;
            owner.player.viewHeight += up / deflectTime;

            psp.x += weaponOfs.x * age / deflectTime;
            psp.y += weaponOfs.y * age / deflectTime;
        }
        else if (age < deflectTime + returnTime)
        {
            owner.angle -= angle / returnTime;
            owner.pitch -= pitch / returnTime;
            owner.roll -= roll / returnTime;
            owner.player.viewHeight -= up / returnTime;

            psp.x += weaponOfs.x * (1.0 - (age - deflectTime) / returnTime);
            psp.y += weaponOfs.y * (1.0 - (age - deflectTime) / returnTime);
        }
        else
        {
            Destroy();
        }
    }
}
